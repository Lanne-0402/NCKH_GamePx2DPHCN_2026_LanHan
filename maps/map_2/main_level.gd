extends Node2D

@export var countdown_step_seconds := 1.0
@export var tracking_loss_grace_seconds := 3.0

const EXERCISE_CONFIG := {
	"exercise_id": "wall_abduction_external_rotation",
	"repetitions_per_phase": 5,
	"phase_count": 2,
	"hold_duration_ms": 5000,
}

var _session_config: Dictionary = {}
var _session_attempt := 0
var _intro_running := false

@onready var exercise_session: ExerciseSession = $ExerciseSession
@onready var map2_controller: Node = $Map2Controller
@onready var tracking_stability: Node = $TrackingStability
@onready var state_label: Label = $UI/StateLabel
@onready var phase_label: Label = $UI/PhaseLabel
@onready var rep_label: Label = $UI/RepLabel
@onready var feedback_label: Label = $UI/FeedbackLabel
@onready var rest_label: Label = $UI/RestLabel
@onready var result_panel: PanelContainer = $UI/ResultPanel
@onready var result_label: Label = $UI/ResultPanel/MarginContainer/VBoxContainer/ResultLabel
@onready var lift_progress_label: Label = $UI/LiftProgressLabel
@onready var hearts_label: Label = $UI/HeartsLabel
@onready var hold_bar: ProgressBar = $UI/HoldPanel/HoldBar
@onready var hold_label: Label = $UI/HoldPanel/HoldLabel
@onready var instruction_panel: PanelContainer = $UI/InstructionPanel
@onready var instruction_start_button: Button = $UI/InstructionPanel/MarginContainer/VBoxContainer/StartExerciseButton
@onready var countdown_label: Label = $UI/CountdownLabel


func _ready() -> void:
	result_panel.hide()
	rest_label.hide()
	countdown_label.hide()
	hold_bar.value = 0.0

	exercise_session.state_changed.connect(_on_session_state_changed)
	exercise_session.progress_changed.connect(_on_session_progress_changed)
	exercise_session.feedback_changed.connect(_on_session_feedback_changed)
	exercise_session.repetition_accepted.connect(_on_repetition_accepted)
	exercise_session.repetition_rejected.connect(_on_repetition_rejected)
	exercise_session.hold_progress_changed.connect(_on_hold_progress_changed)
	exercise_session.hold_interrupted.connect(_on_hold_interrupted)
	exercise_session.rest_time_changed.connect(_on_rest_time_changed)
	exercise_session.session_completed.connect(_on_session_completed)

	tracking_stability.grace_seconds = tracking_loss_grace_seconds
	tracking_stability.setup(exercise_session, PoseInput)
	tracking_stability.stability_changed.connect(_on_stability_changed)
	tracking_stability.tracking_penalty_applied.connect(_on_tracking_penalty_applied)

	map2_controller.lift_progress_changed.connect(_on_lift_progress_changed)
	map2_controller.action_window_changed.connect(_on_action_window_changed)
	map2_controller.hold_visual_progress_changed.connect(_on_hold_visual_progress_changed)

	_session_config = EXERCISE_CONFIG.duplicate(true)
	_session_config["difficulty"] = _difficulty_name()
	_session_config["target_rom_ratio"] = _target_rom_ratio()
	_session_config["minimum_movement_duration_ms"] = 2000
	_session_config["maximum_movement_duration_ms"] = 5000
	_session_config["rest_duration_seconds"] = 30.0
	_session_config["calibrated_max_rom"] = Global.max_rom_angle
	_session_config["target_rom_value"] = Global.max_rom_angle * float(_session_config["target_rom_ratio"]) if Global.max_rom_angle > 0.0 else 0.0

	var total_lifts := int(_session_config["repetitions_per_phase"]) * int(_session_config["phase_count"])
	map2_controller.setup_map(total_lifts)
	tracking_stability.reset()
	_show_intro()


func _unhandled_key_input(event: InputEvent) -> void:
	if event is InputEventKey and event.pressed and not event.echo and event.physical_keycode == KEY_N:
		exercise_session.debug_skip_rest()


func _on_session_state_changed(current: ExerciseSession.State, _previous: ExerciseSession.State) -> void:
	state_label.text = "TRẠNG THÁI: %s" % _state_text(current)
	rest_label.visible = current == ExerciseSession.State.RESTING
	var gameplay_active := current == ExerciseSession.State.ACTIVE
	$ScrollingBackground.set_scrolling(gameplay_active)
	map2_controller.set_session_active(gameplay_active)
	if gameplay_active:
		exercise_session.set_repetition_input_enabled(map2_controller.is_action_window_open())
	elif current == ExerciseSession.State.TRACKING_LOST:
		map2_controller.handle_hold_interrupted()


func _on_session_progress_changed(rep: int, reps_required: int, phase: int, phases: int) -> void:
	rep_label.text = "LẦN: %d/%d" % [rep, reps_required]
	phase_label.text = "HIỆP: %d/%d" % [phase, phases]


func _on_session_feedback_changed(code: String) -> void:
	var display_code := code
	if code == "SESSION_READY" and not map2_controller.is_action_window_open():
		display_code = "SLAB_DESCENDING"
	feedback_label.text = _feedback_text(display_code)
	if display_code in ["REP_VALID", "TRACKING_RESTORED", "ACTION_ZONE_READY", "HOLD_COMPLETED"]:
		feedback_label.modulate = Color(0.1, 0.75, 0.2)
	elif display_code in ["MOVEMENT_TOO_FAST", "REP_REJECTED", "HOLD_INTERRUPTED", "TRACKING_LOST", "BACKEND_DISCONNECTED"]:
		feedback_label.modulate = Color(0.9, 0.2, 0.1)
	else:
		feedback_label.modulate = Color.WHITE


func _on_repetition_accepted(data: Dictionary) -> void:
	map2_controller.handle_valid_repetition(data)


func _on_repetition_rejected(_data: Dictionary) -> void:
	map2_controller.handle_hold_interrupted()


func _on_hold_progress_changed(progress: float, elapsed_ms: int, target_ms: int) -> void:
	hold_bar.value = progress * 100.0
	var elapsed_seconds := float(elapsed_ms) / 1000.0
	var target_seconds := float(target_ms) / 1000.0
	hold_label.text = "GIỮ: %.1f/%.1f GIÂY" % [elapsed_seconds, target_seconds]
	map2_controller.set_hold_progress(progress)


func _on_hold_interrupted(_data: Dictionary) -> void:
	map2_controller.handle_hold_interrupted()


func _on_hold_visual_progress_changed(progress: float) -> void:
	if progress <= 0.0:
		hold_bar.value = 0.0
		hold_label.text = "GIỮ: 0.0/5.0 GIÂY"


func _on_rest_time_changed(seconds_remaining: float) -> void:
	rest_label.text = "NGHỈ: %d GIÂY\nNhấn N để bỏ qua khi debug" % ceili(seconds_remaining)


func _on_session_completed(summary: Dictionary) -> void:
	await map2_controller.play_finish_sequence()
	var stability: int = int(tracking_stability.stability)
	_unlock_next_difficulty(stability)
	var unlock_text := "Chưa mở cấp độ tiếp theo."
	if _difficulty_level() >= 3:
		unlock_text = "Đã hoàn thành cấp độ cao nhất."
	elif stability >= 2:
		unlock_text = "Cấp độ tiếp theo đã sẵn sàng."
	result_label.text = "HOÀN THÀNH MAP 2\n\nLần nâng hợp lệ: %d\nLần chưa hợp lệ: %d\nSố hiệp: %d\nỔn định còn lại: %d/3\nĐánh giá: %s\n%s" % [
		int(summary.get("valid_reps", 0)),
		int(summary.get("rejected_reps", 0)),
		int(summary.get("phase_count", 0)),
		stability,
		_stability_evaluation(stability),
		unlock_text,
	]
	result_panel.show()


func _on_lift_progress_changed(completed: int, total: int) -> void:
	lift_progress_label.text = "ĐÁ ĐÃ NÂNG: %d/%d" % [completed, total]


func _on_stability_changed(current: int, maximum: int) -> void:
	var hearts := ""
	for index in range(maximum):
		hearts += "♥ " if index < current else "♡ "
	hearts_label.text = "ỔN ĐỊNH: %s" % hearts.strip_edges()


func _on_tracking_penalty_applied(_current: int, grace_seconds: float) -> void:
	feedback_label.modulate = Color(0.9, 0.2, 0.1)
	feedback_label.text = "Tracking gián đoạn quá %d giây: giảm 1 điểm Ổn định." % ceili(grace_seconds)


func _on_action_window_changed(is_open: bool) -> void:
	exercise_session.set_repetition_input_enabled(is_open)
	if is_open:
		feedback_label.text = _feedback_text("ACTION_ZONE_READY")


func _show_intro() -> void:
	$ScrollingBackground.set_scrolling(false)
	state_label.text = "TRẠNG THÁI: HƯỚNG DẪN"
	feedback_label.text = "Đọc hướng dẫn và nhấn SẴN SÀNG để bắt đầu."
	instruction_start_button.disabled = false
	instruction_panel.show()


func _on_start_exercise_button_pressed() -> void:
	if _intro_running or exercise_session.state != ExerciseSession.State.IDLE:
		return
	_intro_running = true
	instruction_start_button.disabled = true
	instruction_panel.hide()
	countdown_label.show()
	for value in [3, 2, 1]:
		countdown_label.text = str(value)
		await get_tree().create_timer(countdown_step_seconds, false).timeout
	countdown_label.text = "BẮT ĐẦU"
	await get_tree().create_timer(countdown_step_seconds, false).timeout
	countdown_label.hide()
	_start_new_session_attempt()
	_intro_running = false


func _start_new_session_attempt() -> void:
	_session_attempt += 1
	_session_config["session_id"] = "map2-session-%d-%d" % [Time.get_ticks_msec(), _session_attempt]
	exercise_session.start_session(_session_config)


func _on_result_button_pressed() -> void:
	get_tree().change_scene_to_file("res://ui/screens/level_selection/main_menu.tscn")


func _exit_tree() -> void:
	if is_instance_valid(exercise_session):
		exercise_session.stop_session("scene_changed")


func _difficulty_name() -> String:
	if Global.speed_multiplier >= 2.0: return "advanced"
	if Global.speed_multiplier >= 1.5: return "standard"
	return "gentle"


func _target_rom_ratio() -> float:
	if Global.speed_multiplier >= 2.0: return 0.90
	if Global.speed_multiplier >= 1.5: return 0.80
	return 0.70


func _difficulty_level() -> int:
	if Global.speed_multiplier >= 2.0: return 3
	if Global.speed_multiplier >= 1.5: return 2
	return 1


func _unlock_next_difficulty(stability: int) -> bool:
	if stability < 2 or _difficulty_level() >= 3:
		return false
	var previous_highest: int = Global.get_highest_unlocked_level(2)
	Global.unlock_level_for_action(2, _difficulty_level() + 1)
	return Global.get_highest_unlocked_level(2) > previous_highest


func _stability_evaluation(stability: int) -> String:
	if stability >= 3: return "HOÀN HẢO"
	if stability == 2: return "HOÀN THÀNH"
	return "CẦN CẢI THIỆN ĐỘ ỔN ĐỊNH"


func _state_text(value: ExerciseSession.State) -> String:
	match value:
		ExerciseSession.State.READY: return "CHUẨN BỊ"
		ExerciseSession.State.ACTIVE: return "ĐANG TẬP"
		ExerciseSession.State.RESTING: return "ĐANG NGHỈ"
		ExerciseSession.State.PAUSED: return "TẠM DỪNG"
		ExerciseSession.State.TRACKING_LOST: return "MẤT NHẬN DIỆN"
		ExerciseSession.State.COMPLETED: return "HOÀN THÀNH"
		_: return "CHỜ"


func _feedback_text(code: String) -> String:
	const TEXTS := {
		"SESSION_READY": "Sẵn sàng thực hiện động tác.",
		"SLAB_DESCENDING": "Phiến đá đang hạ xuống vị trí an toàn.",
		"ACTION_ZONE_READY": "Đá đã dừng. Duỗi hai tay lên và bắt đầu giữ.",
		"MOVEMENT_STARTED": "Đã nhận chuyển động.",
		"TARGET_REACHED": "Đúng tư thế. Bắt đầu giữ 5 giây.",
		"HOLD_STARTED": "Giữ nguyên tư thế...",
		"HOLD_PROGRESS": "Tiếp tục giữ chậm và ổn định.",
		"HOLD_COMPLETED": "Đã giữ đủ 5 giây!",
		"HOLD_INTERRUPTED": "Tư thế bị gián đoạn. Hãy thử lại chậm rãi.",
		"REP_VALID": "Tốt lắm! Đã nâng thành công một lần.",
		"MOVEMENT_TOO_FAST": "Cử động quá nhanh. Hãy thực hiện chậm hơn.",
		"REP_REJECTED": "Lần tập chưa hợp lệ.",
		"PHASE_REST": "Hoàn thành hiệp. Hãy nghỉ 30 giây.",
		"NEXT_PHASE": "Bắt đầu hiệp tiếp theo.",
		"TRACKING_LOST": "Không nhìn thấy người chơi. Buổi tập đã tạm dừng.",
		"TRACKING_RESTORED": "Đã nhận diện lại. Tiếp tục buổi tập.",
		"BACKEND_DISCONNECTED": "Mất kết nối với hệ thống nhận diện.",
		"SESSION_COMPLETED": "Hoàn thành Map 2!",
	}
	return TEXTS.get(code, code)
